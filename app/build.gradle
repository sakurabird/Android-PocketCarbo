apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.sakurafish.pockettoushituryou"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
        release {
            storeFile rootProject.file("sakurafish.keystore")
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }

    aaptOptions {
        noCompress 'apk'
    }
}

ext {
    supportLibraryVersion = '25.3.1'
    googleServicesVersion = '11.0.1'
}

configurations {
    all {
        resolutionStrategy {
            force "com.android.support:support-annotations:$supportLibraryVersion"
            force 'com.google.code.findbugs:jsr305:2.0.1'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support Library
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    // Google Searvices
    compile "com.google.firebase:firebase-core:$googleServicesVersion"
    compile "com.google.firebase:firebase-crash:$googleServicesVersion"
    compile "com.google.firebase:firebase-ads:$googleServicesVersion"

    // Structure
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    compile 'io.reactivex.rxjava2:rxjava:2.0.5'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Network
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'org.jsoup:jsoup:1.10.3'

    // DB
    annotationProcessor 'com.github.gfx.android.orma:orma-processor:4.2.4'
    compile 'com.github.gfx.android.orma:orma:4.2.4'

    // UI
    compile 'net.yanzm:loopviewpager:25.0.1'
    compile 'net.cachapa.expandablelayout:expandablelayout:2.8'
    compile 'br.com.mauker.materialsearchview:materialsearchview:1.2.2'
    compile 'com.github.jd-alexander:LikeButton:0.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'

    // Utility
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile('de.psdev.licensesdialog:licensesdialog:1.8.2')
    compile 'com.github.deano2390:MaterialShowcaseView:1.1.0'

    // Test
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
