buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 27

// version code = api version(2) + app version(4) + build number(2)
    defaultConfig {
        applicationId "com.sakurafish.pockettoushituryou"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 17020301
        versionName "2.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
        release {
            storeFile rootProject.file("sakurafish.keystore")
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }

    aaptOptions {
        noCompress 'apk'
    }
}

ext {
    supportLibraryVersion = '27.1.1'
}

configurations {
    all {
        resolutionStrategy {
            force "com.android.support:support-annotations:$supportLibraryVersion"
            force 'com.google.code.findbugs:jsr305:2.0.1'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Support Library
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"

    // Google Services
    implementation "com.google.firebase:firebase-core:16.0.8"
    implementation 'com.google.firebase:firebase-appindexing:17.1.0'
    implementation "com.google.firebase:firebase-crash:16.2.1"
    implementation "com.google.firebase:firebase-ads:15.0.1"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }

    // Structure
    implementation 'com.google.dagger:dagger:2.17'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'org.jsoup:jsoup:1.11.3'

    // DB
    annotationProcessor 'com.github.gfx.android.orma:orma-processor:5.0.0-rc4'
    implementation 'com.github.gfx.android.orma:orma:5.0.0-rc4'

    // UI
    implementation 'br.com.mauker.materialsearchview:materialsearchview:1.2.3'
    implementation 'net.yanzm:loopviewpager:25.0.1'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // Utility
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.2.0'

    // Test
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

// apkファイルの名前を変更
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "PocketCarbo_v${android.defaultConfig.versionName}" +
                "_vc${android.defaultConfig.versionCode}_${variant.name}.apk"
    }
}

// MUST BE AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'