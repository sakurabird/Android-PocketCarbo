plugins {
    // ./gradlew dependencyUpdates で新しいバージョンを検出
    id "com.github.ben-manes.versions" version "0.27.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

def applicationName = "PocketCarbo"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28

// version code = api version(2) + app version(4) + build number(2)
    defaultConfig {
        applicationId "com.sakurafish.pockettoushituryou"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 23020401
        versionName "2.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        archivesBaseName = "${applicationName}-v${versionName}-vc${versionCode}"
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/rxjava.properties'
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            debuggable true
            zipAlignEnabled true
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        textReport true
        textOutput 'stdout'
        lintConfig file("lint.xml")
    }

    aaptOptions {
        noCompress 'apk'
    }

    kapt {
        // https://kotlinlang.org/docs/reference/kapt.html#non-existent-type-correction
        correctErrorTypes = true
    }
}

ext {
    supportLibraryVersion = '1.1.0'
    lifecycleVersion = "2.2.0-rc01"
    daggerVersion = "2.25.2"
    moshiVersion = "1.8.0"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support Library
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.appcompat:appcompat:$supportLibraryVersion"
    implementation 'com.google.android.material:material:1.1.0-beta01'
    implementation 'androidx.browser:browser:1.0.0'

    // architecture components
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Google Services
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-appindexing:19.0.0'
    implementation "com.google.firebase:firebase-ads:18.3.0"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    // dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Data
    kapt 'com.github.maskarade.android.orma:orma-processor:6.0.2'
    implementation 'com.github.maskarade.android.orma:orma:6.0.2'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // UI
    implementation 'br.com.mauker.materialsearchview:materialsearchview:1.2.3'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.afollestad.material-dialogs:core:3.1.1'

    // Utility
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.4'

    // Test
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

// MUST BE AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'